# Hardhat Framework 

Ngoài foundry, chúng ta còn có 1 framework nữa là hardhat framework, một framework để phát triển, test và deploy smart contract.

## Cài đặt

Để sử dụng hardhat, chúng ta cần thiết lập môi trường phát triển và cài đặt hardhat. Hãy thực hiện việc này theo các bước sau:

### Bước 1: Tạo thư mục cho project của bạn

```bash
mkdir hardhat-fundme
cd hardhat-fundme
```

### Bước 2: Khởi tạo node_module

Dán lệnh này vào terminal của bạn để tạo tệp package.json

```bash
npm init -y
```

### Bước 3: Cài đặt hardhat và các dependencies khác

* Dán mã dưới đây vào terminal của bạn để cài đặt hardhat

```bash
npm install --save-dev hardhat
```

* Dán mã dưới đây để cài đặt các dependencies khác

```bash
npm install dotenv @bisonai/orakl-contracts
```

### Bước 4: Khởi tạo một dự án hardhat

```bash
npx hardhat init
```

Hãy chắc chắn làm theo các hướng dẫn được hiển thị trong terminal của bạn. Đối với dự án này, chúng tôi đã chọn *hardhat javascript project* và cài đặt *hardhat-toolbox*.

Sau khi khởi tạo dự án hardhat, thư mục hiện tại của bạn sẽ bao gồm:

```
1. contracts/ – thư mục này chứa smart contract

2. ignition/ – thư mục này chứa các file giúp bạn deploy lên mạng blockchain

3. test/ – thư mục này chưa các file test 

4. hardhat.config.js – file này chứa những tuỳ chỉnh quan trọng của dự án, quy trình deploy và xác mình smart contract trong dự án Hardhat này.
```

### Bước 5: Tạo một file `.env`

Tạo một file `.env` trong project folder của bạn để chứa những biến môi trường.

* Dán lệnh này vào terminal của bạn để tạo file `.env`

```bash
touch .env
```

* Sau khi tạo file, giờ chúng ta thêm nội dung vào file như vầy:

```bash
KAIA_KAIROS_URL= "Your Kairos RPC link"
PRIVATE_KEY= "your private key copied from MetaMask wallet"
```

**Step 6: Điều chỉnh Hardhat config của bạn**

Thay đổi file `hardhat.config.js` với những tuỳ chỉnh sau:

```js
require("@nomicfoundation/hardhat-toolbox");
require('dotenv').config()


module.exports = {
  solidity: "0.8.24",
  networks: {
    kairos: {
      url: process.env.KAIA_KAIROS_URL || "",
      gasPrice: 250000000000,
      accounts:
        process.env.PRIVATE_KEY !== undefined ? [process.env.PRIVATE_KEY] : [],
    }
  },
  etherscan: {
    apiKey: {
      kairos: "unnecessary",
    },
    customChains: [
      {
        network: "kairos",
        chainId: 1001,
        urls: {
          apiURL: "https://api-baobab.klaytnscope.com/api",
          browserURL: "https://baobab.klaytnscope.com",
        },
      },
    ]
  }
};

```

Sau khi chúng ta đã thiết lập xong môi trường Hardhat, hãy bắt đầu viết smart contract FundMe.

## Tạo FundMe Smart Contract

In the contracts folder, you will create `FundMe.sol` and `PriceConverter.sol` file respectively.

**FundMe.sol**

```solidity
// FundMe.sol
// SPDX-License-Identifier: MIT
// 1. Pragma
pragma solidity ^0.8.19;
// 2. Imports
// Chúng ta import thư viện orakl để chúng ta có thể tương tác với oracle
import { IAggregator } from "@bisonai/orakl-contracts/src/v0.1/interfaces/IAggregator.sol";

// Chúng ta import thư viện PriceConverter để chúng ta tính toán giá trị Ether
import { PriceConverter } from "./PriceConverter.sol";

// 3. Interfaces, Libraries, Contracts
// Khai báo error không phải là Owner của contract
error FundMe__NotOwner();

/**
 * @title A sample Funding Contract
 * @author Patrick Collins
 * @notice This contract is for creating a sample funding contract
 * @dev This implements price feeds as our library
 */
contract FundMe {
    // Type Declarations
    // Dòng tiếp theo có nghĩa là 
    // "sử dụng library PriceConverter cho những biến có type là uint256"
    using PriceConverter for uint256;

    // State variables
    // Khai báo 1 public constant MINIMUM_USD với giá trị $5 nhưng bằng wei nên phải nhân 10^18
    uint256 public constant MINIMUM_USD = 5 * 10 ** 18;
    // Khai báo 1 address private và immutable với tên là i_owner, i nghĩa là immutable.
    address private immutable i_owner;
    // Khai báo 1 array private chứa danh sách những người fund ether vào với tên là s_funders, s nghĩa là storage.
    address[] private s_funders;
    // Khai báo 1 mapping giữa address với uint256 private liên kết địa chỉ với số tiền fund.
    mapping(address => uint256) private s_addressToAmountFunded;
    // Khai báo contract AggregatorV3Interface private và gán vào biến s_pricefeed, s nghĩa là storage
    IAggregator private s_priceFeed;

    // Events (we have none!)

    // Modifiers
    // Khai báo 1 modifier onlyOwner để gán vào function mà chỉ owner có thể gọi được
    modifier onlyOwner() {
        // require(msg.sender == i_owner);
        if (msg.sender != i_owner) revert FundMe__NotOwner();
        _;
    }

    // Functions Order:
    //// constructor
    //// receive
    //// fallback
    //// external
    //// public
    //// internal
    //// private
    //// view / pure

    // Khai báo constructor với 1 địa chỉ cho priceFeed ám chỉ rằng đây là địa chỉ của contract Oracle với IAggregator
    constructor(address priceFeed) {
        // input địa chỉ vào interface và gán vào biến s_priceFeed
        s_priceFeed = IAggregator(priceFeed);
        // Gán biến i_owner là msg.sender (người deploy contract này)
        i_owner = msg.sender;
    }

    /// @notice Funds our contract based on the KLAY/USDT price from Orakl
       // Gửi tiền vào contract của chúng ta dựa trên giá ETH/USD
    function fund() public payable {
        require(msg.value.getConversionRate(s_priceFeed) >= MINIMUM_USD, "You need to spend more ETH!");
        // require(PriceConverter.getConversionRate(msg.value) >= MINIMUM_USD, "You need to spend more ETH!");
        // Sau đó map địa chỉ của người gửi với msg.value trong mapping s_addressToAmountFunded
        s_addressToAmountFunded[msg.sender] += msg.value;
        // Sau đó thêm địa chỉ người gửi vào danh sách các funders
        s_funders.push(msg.sender);
    }

    function withdraw() public onlyOwner {
        // dùng for loop, bắt đàu từ index 0 đến index ít hơn length của danh sách, và index cộng 1 cho mỗi vòng loop
        for (uint256 funderIndex = 0; funderIndex < s_funders.length; funderIndex++) {
            // gán giá trị address tại funderIndex trong danh sách s_funders vào address funder
            address funder = s_funders[funderIndex];
            // thay đổi giá trị của mapping s_addressToAmountFunded có address là funder thành 0, tức là funder này đã withdraw
            s_addressToAmountFunded[funder] = 0;
        }
        // tạo một danh sách s_funders mới với 1 dynamic array (nôm na là danh sách) mới với kích cỡ bằng 0
        s_funders = new address[](0);
        // Transfer vs call vs Send
        // Transfer vs call vs Send
        // - transfer (2300 gas, throws error if any)
        // - send (2300 gas, returns bool for success or failure)
        // - call (forward all gas or set gas, returns bool for success or failure)
        // payable(msg.sender).transfer(address(this).balance);

        // Gửi toàn bộ balance của contract này tới i_owner và không có data trong transaction và trả về boolean success hay không
        (bool success,) = i_owner.call{value: address(this).balance}("");
        // Yêu cầu bool success true nếu không thì revert toàn bộ        
        require(success);
    }

    function cheaperWithdraw() public onlyOwner {
        // Copy danh sách s_funders từ storage vào memory, tức là load từ global state vào local state. Thay đổi global state tốn nhiều gas hơn local state
        address[] memory funders = s_funders;
        // mappings can't be in memory, sorry!
        for (uint256 funderIndex = 0; funderIndex < funders.length; funderIndex++) {
            address funder = funders[funderIndex];
            s_addressToAmountFunded[funder] = 0;
        }
        s_funders = new address[](0);
        // payable(msg.sender).transfer(address(this).balance);
        (bool success,) = i_owner.call{value: address(this).balance}("");
        require(success);
    }

    /** Getter Functions */
    // Những function chỉ dùng để GET thông tin
    /**
     * @notice Gets the amount that an address has funded
     *  @param fundingAddress the address of the funder
     *  @return the amount funded
     */
    function getAddressToAmountFunded(address fundingAddress) public view returns (uint256) {
        return s_addressToAmountFunded[fundingAddress];
    }

    /**
     * @notice Gets the funder at a specific index
     * @param index the index of the funder
     * @return the address of the funder
     */
    function getFunder(uint256 index) public view returns (address) {
        return s_funders[index];
    }

    /// @notice Gets the owner of the contract
    function getOwner() public view returns (address) {
        return i_owner;
    }

    /// @notice Gets the price feed
    function getPriceFeed() public view returns (IAggregator) {
        return s_priceFeed;
    }

    /// @notice Gets the decimals of the price feed
    function getDecimals() public view returns (uint8) {
        return s_priceFeed.decimals();
    }

    /// @notice Gets the description of the price feed
    function getDescription() public view returns (string memory) {
        return s_priceFeed.description();
    }
}
```

**PriceConverter.sol**

```solidity
// PriceConverter.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

// import IAggregator từ orakl repository
import { IFeedProxy } from "@bisonai/orakl-contracts/v0.2/src/interfaces/IFeedProxy.sol";


// Declare a library named PriceConverter
library PriceConverter {
    
    // Declare function getPrice with input as contract interface and return uint256
    function getPrice(IFeedProxy dataFeed) internal view returns (uint256) {
        // gọi function latestRoundData() trong priceFeed
        (, int256 answer,) = dataFeed.latestRoundData();
        // Returns the ETH/USD rate with 18 digits (Oracle has 8 zeros so add 10 zeros)
        // ETH/USD rate in 18 digit
        return uint256(answer * 10000000000);
    }

    // 1000000000
    // call it get fiatConversionRate, since it assumes something about decimals
    // It wouldn't work for every aggregator
    // Convert KLAY amount to USD amount
    // function getConversionRate takes input ethAmount with type uint256 and interface contract, returns uint256
    function getConversionRate(uint256 ethAmount, IFeedProxy dataFeed) internal view returns (uint256) {
        // First get the eth price using getPrice and assign it to the variable ethPrice
        uint256 ethPrice = getPrice(dataFeed);
        // Then multiply ethPrice by the amount of ether and divide by 18 zeros
        // In solidity, we should multiply before dividing because there is no float
        // This calculation is ethPrice (18 digits) * ethAmount (18 digits) / 18 digits to get back 18 digits.      
        uint256 ethAmountInUsd = (ethPrice * ethAmount) / 1000000000000000000;
        // Returns the USD value of the ether amount    
        // the actual ETH/USD conversation rate, after adjusting the extra 0s.
        return ethAmountInUsd;
    }
}
```

## Testing FundMe Smart Contract

**Step 1:** In the Explorer pane, select the test folder and click the New File button to create a new file named `Fundme.js`

**Step 2:** Create a `MockDataFeedAggregator.sol` in the contract folder for testing purposes. Copy and paste the code below in this file:

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/**
 * @title MockV3Aggregator
 * @notice Based on the FluxAggregator contract
 * @notice Use this contract when you need to test
 * other contract's ability to read data from an
 * aggregator contract, but how the aggregator got
 * its answer is unimportant
 */
contract MockDataFeedAggregator {
    uint256 public constant version = 4;

    uint8 public decimals;
    int256 public latestAnswer;
    uint256 public latestTimestamp;
    uint256 public latestRound;

    mapping(uint256 => int256) public getAnswer;
    mapping(uint256 => uint256) public getTimestamp;
    mapping(uint256 => uint256) private getStartedAt;

    constructor(uint8 _decimals, int256 _initialAnswer) {
        decimals = _decimals;
        updateAnswer(_initialAnswer);
    }

    function updateAnswer(int256 _answer) public {
        latestAnswer = _answer;
        latestTimestamp = block.timestamp;
        latestRound++;
        getAnswer[latestRound] = _answer;
        getTimestamp[latestRound] = block.timestamp;
        getStartedAt[latestRound] = block.timestamp;
    }

    function updateRoundData(uint80 _roundId, int256 _answer, uint256 _timestamp, uint256 _startedAt) public {
        latestRound = _roundId;
        latestAnswer = _answer;
        latestTimestamp = _timestamp;
        getAnswer[latestRound] = _answer;
        getTimestamp[latestRound] = _timestamp;
        getStartedAt[latestRound] = _startedAt;
    }

    function getRoundData(uint80 _roundId)
        external
        view
        returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
    {
        return (_roundId, getAnswer[_roundId], getStartedAt[_roundId], getTimestamp[_roundId], _roundId);
    }

    function latestRoundData()
        external
        view
        returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
    {
        return (
            uint80(latestRound),
            getAnswer[latestRound],
            getStartedAt[latestRound],
            getTimestamp[latestRound],
            uint80(latestRound)
        );
    }

    function description() external pure returns (string memory) {
        return "v0.6/test/mock/MockV3Aggregator.sol";
    }
}
```

**Step 3**: Copy and paste the code below in `Fundme.js` file:

```
// Fundme.js

const { expect } = require("chai");
const { ethers } = require("hardhat");
const { loadFixture } = require("@nomicfoundation/hardhat-toolbox/network-helpers");

describe("FundMe", function () {
  async function deployContractsFixture() {
    const [deployer, addr1, addr2] = await ethers.getSigners();

    const MockDataFeedAggregator = await ethers.getContractFactory("MockDataFeedAggregator");
    const mockPriceFeed = await MockDataFeedAggregator.connect(deployer).deploy(8, 2000 * 10 ** 8); 
    // Example price of 2000 USD with 8 decimals
    await mockPriceFeed.waitForDeployment(); // Ensure the contract is deployed

    // Use fully qualified name for FundMe contract
    const FundMe = await ethers.getContractFactory("contracts/FundMe.sol:FundMe");
    const fundMe = await FundMe.connect(deployer).deploy(mockPriceFeed.target);
    await fundMe.waitForDeployment(); // Ensure the contract is deployed

    return { fundMe, mockPriceFeed, deployer, addr1, addr2 };
  }

  describe("Deployment", function () {
    it("Should set the right owner", async function () {
      const { fundMe, deployer } = await loadFixture(deployContractsFixture);
      expect(await fundMe.getOwner()).to.equal(deployer.address);
    });
  });

  describe("Fund", function () {
    it("Should accept funds", async function () {
      const { fundMe, addr1 } = await loadFixture(deployContractsFixture);
      const sendValue = ethers.parseEther("1"); // 1 ETH
      await fundMe.connect(addr1).fund({ value: sendValue });

      expect(await fundMe.getAddressToAmountFunded(addr1.address)).to.equal(sendValue);
    });

    it("Should require a minimum amount in USD", async function () {
      const { fundMe, addr1 } = await loadFixture(deployContractsFixture);
      const sendValue = ethers.parseEther("0.001"); // 0.001 ETH, less than minimum

      await expect(fundMe.connect(addr1).fund({ value: sendValue })).to.be.revertedWith(
        "You need to spend more ETH!"
      );
    });
  });

  describe("Withdraw", function () {
    it("Should withdraw ETH correctly", async function () {
      const { fundMe, addr1 } = await loadFixture(deployContractsFixture);
      const sendValue = ethers.parseEther("1"); // 1 ETH
      await fundMe.connect(addr1).fund({ value: sendValue });

      await fundMe.withdraw();
      expect(await ethers.provider.getBalance(fundMe.target)).to.equal(0);
    });

    it("Should only allow the owner to withdraw", async function () {
      const { fundMe, addr1 } = await loadFixture(deployContractsFixture);
      const sendValue = ethers.parseEther("1"); // 1 ETH
      await fundMe.connect(addr1).fund({ value: sendValue });

      await expect(fundMe.connect(addr1).withdraw()).to.be.revertedWithCustomError(
        fundMe,
        "FundMe__NotOwner"
      );
    });
  });
});

```

**Step 4**: To run your test, open up your terminal and run the command below:

```
npx hardhat test test/Fundme.js
```

## Deploying the smart contract

**Step 1**: In the Explorer pane, select the `Ignition/modules` folder and click the New File button to create a new file named `Fundme.js`

**Step 2:** Copy and paste the following code inside the file.

```
const { buildModule } = require("@nomicfoundation/hardhat-ignition/modules");

const PRICE_FEED_ADDRESS = "0x1408cb13d84ba8cb533fdf332db5d78290b071c9";

module.exports = buildModule("FundMeModule", (m) => {
  const priceFeedAddr = m.getParameter("_priceFeed", PRICE_FEED_ADDRESS);
  const fundMe = m.contract("FundMe", [priceFeedAddr], {});
  return { fundMe };
});

```

**Step 3**: In the terminal, run the following command which tells Hardhat to deploy your Fundme contract on the Kaia Testnet (Kairos)

```
npx hardhat ignition deploy ignition/modules/Fundme.js --network kairos
```

## Verifying the smart contract

Copy and paste the code below in your terminal:

```
// example
// npx hardhat verify –network <network> <deployed_address> <parameters>

npx hardhat verify --network kairos 0xa9a6f38b67d7ba2b716d0d2bd21974e2149df7ef 0xf0d6Ccdd18B8A7108b901af872021109C27095bA
```
